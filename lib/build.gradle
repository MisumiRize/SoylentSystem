buildscript {
    ext.kotlin_version = '0.12.613'
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
    }
}
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'

repositories {
    jcenter()
    mavenCentral()
}

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 22
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

group = 'org.soylentsystem'
archivesBaseName = 'soylentsystem'
version = '0.1-alpha1'

def properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.key')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'SoylentSystem'
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task mainPom << {
    pom {
        project {
            packaging 'aar'
            artifactId archivesBaseName
            name 'SoylentSystem'
            url 'https://github.com/MisumiRize/SoylentSystem'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id 'misumirize'
                    name 'Rize MISUMI'
                    email 'r@ayase-e.li'
                }
            }
            scm {
                connection 'https://github.com/MisumiRize/SoylentSystem.git'
                developerConnection 'https://github.com/MisumiRize/SoylentSystem.git'
                url 'https://github.com/MisumiRize/SoylentSystem'
            }
        }
    }.writeTo("$buildDir/${archivesBaseName}.pom")
}

artifacts {
    archives sourcesJar
    archives file: file("$buildDir/${archivesBaseName}.pom"), builtBy: mainPom
}
